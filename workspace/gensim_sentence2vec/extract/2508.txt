
中心から離れるほど直線的に下降する乱数を生成するには?
[Top]  
[投稿ルール]
[新規質問]
[このトピックに発言]
投稿者:しげポン
投稿日:2009/06/21(日) 17:38:51 No.1
rnd関数は均等に乱数が発生しますが 均等でなく中心から離れるほど
直線的に下降するようなものを作成したいのですが思うようなものが出来ません。
例えば 乱数の中心が 300点で ±200点のバラツキが生じるようなもので
中心の 300点ぐらいが一番発生確立が高く
半分の200点と400点は 300点の 50%程度
両端の100と500は ほぼ 0 というふうに
中心から離れるほど直線的に下降するものです。
発生確率をグラフで描くと ＿＿ になるようなものをイメージしています。
2乗カーブ 300±(rnd(x)*rnd(x))/x 等でやってみましたが中心付近に
集中しすぎて どうも うまくいきませんでした。
ご教授 よろしくお願いします。
投稿者:ぷま
[HomePage]
投稿日:2009/06/21(日) 20:15:26 No.2
> 例えば 乱数の中心が 300点で ±200点のバラツキが生じるようなもの
r= 300+ rnd.200-rnd.200  で
乱数rは 300を中心とする±199の 三角分布になります。
上記検証用として以下スクリを作成してみました。
乱数をn回(100000回)発生させ発生回数(確率)をグラフにしてみたものです。
n= 100000	; 乱数発生回数
c= 300		; 中心
m= 200		; 範囲±
mes "n= "+ n
randomize
dim y, 640												// グラフに描くための配列確保
repeat n
r= c+rnd.m-rnd.m									// cを中心とする三角分布乱数生成
;r= c-m+1+rnd(m*2)									// 通常のランダム
y.r++ : if max< y.r : max++ : rmax= r : await		// 発生回数をカウント
loop
/******** 以上の結果を 棒グラフにしてみる ********/
title "発生頻度計測  "+c + " ±"+(m-1) + " 最多値 "+ rmax+" ⇒ "+max+ "回"
redraw 0
pos ,100 : mes "\n%" : pos 600,420 : mes "値"
repeat 640 ; m*2, c-m									// 確率 棒グラフ
line cnt, 400, cnt, 400 -250.* y.cnt/max
loop
color ,55,255
w= 50
repeat 6												// 線引き
pos 0,400-cnt*w : mes cnt*1000/w
line ginfo_sx, 400 -cnt*w, 0, 400 -cnt*w
loop
repeat ginfo_winx/w+1
pos cnt*w, 400 : mes cnt*w
line cnt*w, 400 , cnt*w, 150
loop
redraw
投稿者:しげポン
投稿日:2009/06/22(月) 02:57:18 No.3
解決!!(≧≦)
ありがとうございました。
お蔭様で期待通りのものを実装することが出来ました。
つくづく 格の差を痛感させられてしまいました。ショボーン;;(笑)
追加発言 []  
お名前
中心から離れるほど直線的に下降する乱数を生成するには?
